##################################################################################################################################
# 1 - What are the main topics, categories or variables represented in a dataset?
##################################################################################################################################

PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?description 
(GROUP_CONCAT(DISTINCT ?subjectLabel;SEPARATOR=',\n') AS ?topics)
(GROUP_CONCAT(DISTINCT ?variableLabel;SEPARATOR=',\n') AS ?variables)
WHERE {
    ?s				a					qb:DataSet ;
                    dct:description		?description ;
                    dct:subject 		?subject ;
                    qb:structure		[
                    		qb:component	?component 
    ] .
    
    ?subject 		skos:prefLabel 		?subjectLabel .	
    
    ?component		qb:dimension 		?dimension .
    ?dimension 		qb:concept			?variable .
    ?variable		rdfs:label | skos:prefLabel 		?variableLabel .
    FILTER (lang(?variableLabel) = 'en')
    
    VALUES ?s { <http://www.canada-open-data.org/example-aggregated-dataset> }
}
GROUP BY ?description


##################################################################################################################################
# 1.1 - What other datasets represent the same topics, categories or variables?
##################################################################################################################################

PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX dct: <http://purl.org/dc/terms/>

SELECT DISTINCT ?otherDataset ?title
WHERE {
    ?otherDataset		a 				qb:DataSet ;
                   		dct:title 		?title ;
                   		dct:subject		?subject .
    
    FILTER(?otherDataset != ?dataset)

    {
        SELECT DISTINCT ?dataset ?subject ?variable
        WHERE {
            ?dataset		a					qb:DataSet ;
                            dct:subject 		?subject ;
                            qb:structure		[
                                    qb:component	?component 
            ] .
            ?component		qb:dimension 		?dimension .
            ?dimension 		qb:concept			?variable .
            VALUES ?dataset { <http://www.canada-open-data.org/example-aggregated-dataset> }
        }
    }
}


##################################################################################################################################
# 1.2 - Are there any conceptual relationship between columns?
##################################################################################################################################

PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX dct: <http://purl.org/dc/terms/>

SELECT DISTINCT ?component ?variable
WHERE {
    ?dataset		a					qb:DataSet ;
              dct:subject 		?subject ;
              qb:structure		[
					qb:component	?component 
			  ] .

    ?component		qb:dimension 		?dimension .
    ?dimension 		qb:concept			?variable .

    VALUES ?dataset { <http://www.uk-open-data.org/example-aggregated-dataset> }
}


##################################################################################################################################
# 1.3 - Given a variable, which columns are used to represent measurements of this variable in the various datasets?
##################################################################################################################################

PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX sdmx-concept: <http://purl.org/linked-data/sdmx/2009/concept#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?dataset ?title ?component ?componentLabel ?componentDescription
WHERE {
    ?dataset		a					qb:DataSet ;
                    dct:title			?title ;
                    qb:structure		[
                        qb:component	?component 
                    ] .

    ?component		qb:dimension 		?dimension ;
                	rdfs:label			?componentLabel ;
                 	rdfs:description	?componentDescription .
    ?dimension 		qb:concept			?variable .

    VALUES ?variable { sdmx-concept:age }
}


##################################################################################################################################
# 2 - What quantitative insights, such as number of columns and dataset completeness, can be derived from a dataset?
##################################################################################################################################

PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT ?p ?o
WHERE {
    ?dataset		a						qb:DataSet ;
              		dsv:summaryStatistics	[
        				?p ?o
    				] .
    VALUES ?dataset { <http://www.canada-open-data.org/example-aggregated-dataset> }
}


##################################################################################################################################
# 2.1 - How many entries does the dataset have?
##################################################################################################################################

PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT ?numberOfRows 
WHERE {
    ?dataset		a						qb:DataSet ;
              		dsv:summaryStatistics	[
        				dsv:numberOfRows		?numberOfRows 
    				] .
    VALUES ?dataset { <http://www.canada-open-data.org/example-aggregated-dataset> }
}


##################################################################################################################################
# 2.2 - If there is any missing data, how is it encoded? 
##################################################################################################################################

PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT ?datasetCompleteness ?missingValuesFormat
WHERE {
    ?dataset		a						qb:DataSet ;
              		dsv:summaryStatistics	[
        				dsv:datasetCompleteness		?datasetCompleteness ;
            			dsv:missingValuesFormat		?missingValuesFormat
    				] .
    VALUES ?dataset { <http://www.canada-open-data.org/example-aggregated-dataset> }
}


##################################################################################################################################
# 2.3 - And how is the missing data distributed across columns?
##################################################################################################################################

PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX dsv: <https://w3id.org/dsv-ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?component ?componentLabel ?componentDescription ?componentCompleteness
WHERE {
    ?dataset		a						qb:DataSet ;
              		qb:structure			?datasetStructure .
    
    ?datasetStructure	qb:component		?component .
    
    ?component		rdfs:label				?componentLabel ;
                	rdfs:description 		?componentDescription ;
        			dsv:summaryStatistics	[
        				dsv:componentCompleteness		?componentCompleteness
    				] .
    
    VALUES ?dataset { <http://www.canada-open-data.org/example-aggregated-dataset> }
}


##################################################################################################################################
# 2.4 - Are there any potential sources of bias in the dataset?
##################################################################################################################################

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT *
WHERE {
    <http://example.org/ns#uk-codebook/Gender> skos:narrower ?narrowerConcept .
    ?narrowerConcept rdfs:label ?label 
}

##################################################################################################################################
# 3 - Does the dataset have any primary keys? If yes, are the keys shared with other datasets?
##################################################################################################################################

PREFIX csvw: <http://www.w3.org/ns/csvw#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?primaryKey	?label	?description
WHERE {
    ?dataset		csvw:primaryKey		?primaryKey .
    
    ?primaryKey 	rdfs:label 			?label ;
                 	rdfs:description	?description .
    
    VALUES ?dataset { <http://www.nl-open-data.org/example-aggregated-dataset> }
}


##################################################################################################################################
# 3.1 - Can two given datasets with the same primary keys be merged?
##################################################################################################################################

PREFIX csvw: <http://www.w3.org/ns/csvw#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX qb: <http://purl.org/linked-data/cube#>

SELECT DISTINCT *
WHERE {
    ?otherDataset		a					qb:DataSet ;
                   		csvw:primaryKey		?otherPrimaryKey .
    ?otherPrimaryKey	rdfs:label 			?otherLabel ;
                     	rdfs:description 	?otherDescription ;
                      	qb:dimension 		?otherDimension .
    ?otherDimension 	qb:concept			?otherConcept .
    
    FILTER(?otherDataset != ?dataset)
    FILTER(?otherConcept = ?concept)
    {
        SELECT ?dataset ?primaryKey ?dimension ?label ?description ?concept
        WHERE {
            ?dataset		a					qb:DataSet ;
                      		csvw:primaryKey		?primaryKey .
            ?primaryKey		rdfs:label 			?label ;
                         	rdfs:description 	?description ;
                			qb:dimension 		?dimension .
            ?dimension 		qb:concept			?concept .
        }
    }
}

##################################################################################################################################
# 3.2 - How would the metadata description of the merged datasets look like?
##################################################################################################################################


##################################################################################################################################
# 4 - In what ways is the content of two given datasets related?
##################################################################################################################################

PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>

SELECT DISTINCT 
?dataset ?title ?description ?spatialCoverage ?temporalCoverage (GROUP_CONCAT(DISTINCT ?subject;SEPARATOR=',\n') AS ?datasetSubjects)
?otherDataset ?otherTitle ?otherDescription ?otherSpatialCoverage ?otherTemporalCoverage (GROUP_CONCAT(DISTINCT ?otherSubject;SEPARATOR=',\n') AS ?otherDatasetSubjects)
WHERE {
    ?dataset		a						qb:DataSet ;
              		dct:title				?title ;
                	dct:description			?description ;
                 	schema:spatialCoverage	?spatialCoverage ;
                  	schema:temporalCoverage	?temporalCoverage ;
                   	dct:subject				?subject .
    ?otherDataset	a 						qb:DataSet ;
                  	dct:title				?otherTitle ;
                   	dct:description 		?otherDescription ;
                    schema:spatialCoverage	?otherSpatialCoverage ;
                    schema:temporalCoverage	?otherTemporalCoverage ;
                    dct:subject				?otherSubject .
    VALUES ?dataset { <http://www.usa-open-data.org/example-aggregated-dataset>}
    VALUES ?otherDataset { <http://www.uk-open-data.org/example-aggregated-dataset> }
}
GROUP BY ?dataset ?title ?description ?spatialCoverage ?temporalCoverage ?otherDataset ?otherTitle ?otherDescription ?otherSpatialCoverage ?otherTemporalCoverage 

----

PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?component ?dimension ?label ?description ?concept ?otherComponent ?otherDimension ?otherLabel ?otherDescription ?otherConcept

WHERE {
    ?dataset		a						qb:DataSet ;
              		qb:structure			?datasetStructure .
	?datasetStructure	qb:component		?component .
	?component		qb:dimension 			?dimension ;
					rdfs:label 				?label ;
					rdfs:description 		?description .
	?dimension 		qb:concept 				?concept .

    ?otherDataset	a 						qb:DataSet ;
                  	qb:structure			?otherDatasetStructure .
	?otherDatasetStructure	qb:component	?otherComponent .
	?otherComponent	qb:dimension 			?otherDimension ;
					rdfs:label				?otherLabel ;
					rdfs:description 		?otherDescription .
	?otherDimension qb:concept				?otherConcept .
    
    FILTER(?concept = ?otherConcept)
    
    VALUES ?dataset { <http://www.usa-open-data.org/example-aggregated-dataset>}
    VALUES ?otherDataset { <http://www.uk-open-data.org/example-aggregated-dataset> }
}


##################################################################################################################################
# 4.1 - Can two given datasets be joined, so that information of one can be combined with the information of the other one?
##################################################################################################################################

