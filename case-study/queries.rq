##################################################################################################################################
# 1 - What are the main topics and variables represented in a dataset?
##################################################################################################################################

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dsv: <https://w3id.org/dsv-ontology#>


SELECT DISTINCT ?description 
(GROUP_CONCAT(DISTINCT ?subjectLabel;SEPARATOR=',\n') AS ?topics)
(GROUP_CONCAT(DISTINCT ?variableLabel;SEPARATOR=',\n') AS ?variables)
WHERE {
    ?dataset		a					dsv:Dataset ;
                    dct:description		?description ;
                    dct:subject 		?subject ;
                    dsv:datasetSchema		[
                    		dsv:column	?column
    ] .
    
    ?subject 		skos:prefLabel 		?subjectLabel .	
    
    ?column			dsv:columnProperty 	?property .
    ?property 		dsv:hasVariable		?variable .
    ?variable		rdfs:label | skos:prefLabel 		?variableLabel .
    FILTER (lang(?variableLabel) = 'en')
    
    VALUES ?dataset { <http://www.canada-open-data.org/example-dataset> }
}
GROUP BY ?description


##################################################################################################################################
# 2 - What other datasets represent the same topics, categories or variables?
##################################################################################################################################

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT DISTINCT ?relatedDataset ?title
WHERE {
    ?relatedDataset		a					dsv:Dataset ;
                   		dct:title 			?title ;
                   		dct:subject			?subject ;
                     	dsv:datasetSchema	[
                    		dsv:column		?otherColumn
    ] .
    ?otherColumn		dsv:columnProperty 	?otherProperty .
    ?otherProperty	 	dsv:hasVariable 	?variable .
    
    FILTER(?relatedDataset != ?dataset)

    {
        SELECT DISTINCT ?dataset ?subject ?variable
        WHERE {
            ?dataset		a					dsv:Dataset ;
                            dct:subject 		?subject ;
                            dsv:datasetSchema	[
                    			dsv:column		?column
    		] .
            ?column			dsv:columnProperty 	?property .
            ?property 		dsv:hasVariable		?variable .
            VALUES ?dataset { <http://www.canada-open-data.org/example-dataset> }
        }
    }
}


##################################################################################################################################
# 3 - Are there any conceptual relationship between columns?
##################################################################################################################################

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT DISTINCT ?column ?variable
WHERE {
    ?dataset		a					dsv:Dataset ;
              		dct:subject 		?subject ;
              		dsv:datasetSchema	[
                    			dsv:column		?column
    ] .

    ?column			dsv:columnProperty 	?property .
    ?property 		dsv:hasVariable		?variable .

    VALUES ?dataset { <http://www.uk-open-data.org/example-dataset> }
}


##################################################################################################################################
# 4 - Given a variable, which columns are used to represent measurements of this variable in the various datasets?
##################################################################################################################################

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX sdmx-concept: <http://purl.org/linked-data/sdmx/2009/concept#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT DISTINCT ?dataset ?columnLabel ?columnDescription
WHERE {
    ?dataset		a					dsv:Dataset ;
              		dct:subject 		?subject ;
              		dsv:datasetSchema	[
        				dsv:column		?column
    ] .

    ?column 		dsv:columnProperty 	?property ;
                	rdfs:label			?columnLabel ;
                 	rdfs:description	?columnDescription .
    ?property 		dsv:hasVariable		sdmx-concept:age .
}


##################################################################################################################################
# 5 - What quantitative insights, such as number of columns and dataset completeness, can be derived from a dataset?
##################################################################################################################################

PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT ?p ?o
WHERE {
    ?dataset		a					dsv:Dataset ;
              		dsv:summaryStatistics	[
        				?p ?o
    				] .
    VALUES ?dataset { <http://www.canada-open-data.org/example-dataset> }
}


##################################################################################################################################
# 6 - How many entries does the dataset have?
##################################################################################################################################

PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT ?numberOfRows 
WHERE {
    ?dataset		a						dsv:Dataset ;
              		dsv:summaryStatistics	[
        				dsv:numberOfRows		?numberOfRows 
    				] .
    VALUES ?dataset { <http://www.canada-open-data.org/example-dataset> }
}


##################################################################################################################################
# 7 - If there is any missing data, how is it encoded? 
##################################################################################################################################

PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT ?datasetCompleteness ?missingValuesFormat
WHERE {
    ?dataset		a						dsv:Dataset ;
              		dsv:summaryStatistics	[
        				dsv:datasetCompleteness		?datasetCompleteness ;
            			dsv:missingValuesFormat		?missingValuesFormat
    				] .
    VALUES ?dataset { <http://www.canada-open-data.org/example-dataset> }
}


##################################################################################################################################
# 8 - And how is the missing data distributed across columns?
##################################################################################################################################

PREFIX dsv: <https://w3id.org/dsv-ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?column ?columnLabel ?columnDescription ?columnCompleteness
WHERE {
    ?dataset		a						dsv:Dataset ;
              		dsv:datasetSchema		[
        				dsv:column			?column
    ] .
    
    ?column			rdfs:label				?columnLabel ;
                	rdfs:description 		?columnDescription ;
        			dsv:summaryStatistics	[
        				dsv:columnCompleteness ?columnCompleteness
    				] .
    
    VALUES ?dataset { <http://www.canada-open-data.org/example-dataset> }
}


##################################################################################################################################
# 9 - Are there any potential sources of bias in the dataset?
##################################################################################################################################

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT *
WHERE {
    <http://example.org/ns#uk-codebook/Gender> skos:narrower ?narrowerConcept .
    ?narrowerConcept rdfs:label ?label 
}

##################################################################################################################################
# 10 - Does the dataset have any primary keys? If yes, are the keys shared with other datasets?
##################################################################################################################################

PREFIX csvw: <http://www.w3.org/ns/csvw#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sdmx-concept: <http://purl.org/linked-data/sdmx/2009/concept#>
PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT ?dataset ?primaryKey ?label ?description
WHERE {
    ?dataset		csvw:primaryKey		?primaryKey .
    
    ?primaryKey 	rdfs:label 			?label ;
                 	rdfs:description	?description ;
                  	dsv:columnProperty 	[
        				dsv:hasVariable		sdmx-concept:refPeriod ] .
}


##################################################################################################################################
# 11 - Can two given datasets with the same primary keys be merged?
##################################################################################################################################

PREFIX csvw: <http://www.w3.org/ns/csvw#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT DISTINCT *
WHERE {
    ?otherDataset		a					dsv:Dataset ;
                   		csvw:primaryKey		?otherPrimaryKey .
    ?otherPrimaryKey	rdfs:label 			?otherLabel ;
                     	rdfs:description 	?otherDescription ;
                      	dsv:columnProperty 	[
        					dsv:hasVariable		?otherVariable ] .
    
    FILTER(?otherDataset != ?dataset)
    FILTER(?otherVariable = ?variable)
    
    {
        SELECT ?dataset ?primaryKey ?dimension ?label ?description ?variable
        WHERE {
            ?dataset		a					dsv:Dataset ;
                      		csvw:primaryKey		?primaryKey .
            ?primaryKey		rdfs:label 			?label ;
                         	rdfs:description 	?description ;
                			dsv:columnProperty 	[
                				dsv:hasVariable		?variable ] .
        }
    }
}

##################################################################################################################################
# 12 - How would the metadata description of the merged datasets look like?
##################################################################################################################################

PREFIX schema: <http://schema.org/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT DISTINCT 
(GROUP_CONCAT(DISTINCT ?spatialCoverage;SEPARATOR=',\n') AS ?mergedSpatialCoverage)
(GROUP_CONCAT(DISTINCT ?temporalCoverage;SEPARATOR=',\n') AS ?mergedTemporalCoverage)
(GROUP_CONCAT(DISTINCT ?topicLabel;SEPARATOR=',\n') AS ?mergedTopicLabel)
(GROUP_CONCAT(DISTINCT ?variableLabel;SEPARATOR=',\n') AS ?mergedVariable)
WHERE {
    {
        ?dataset1 	schema:spatialCoverage 	?spatialCoverage ;
              		schema:temporalCoverage	?temporalCoverage ;
              		dct:subject	 			[
            			skos:prefLabel			?topicLabel ] ;
                	dsv:datasetSchema	[
            			dsv:column	[
            				dsv:columnProperty	[
            					dsv:hasVariable		[
            						rdfs:label		?variableLabel
        ] ] ] ] . }
    UNION
    {
    	?dataset2 	schema:spatialCoverage 	?spatialCoverage ;
              		schema:temporalCoverage	?temporalCoverage ;
              		dct:subject	 			[
            			skos:prefLabel			?topicLabel ] ;
                	dsv:datasetSchema	[
            			dsv:column	[
            				dsv:columnProperty	[
            					dsv:hasVariable		[
            						rdfs:label		?variableLabel
        ] ] ] ] . }
    
    VALUES ?dataset1 {<http://www.canada-open-data.org/example-dataset>}
    VALUES ?dataset2 {<http://www.uk-open-data.org/example-dataset>}
}

##################################################################################################################################
# 13 - In what ways is the content of two given datasets related?
##################################################################################################################################

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT DISTINCT 
?dataset ?title ?description ?spatialCoverage ?temporalCoverage (GROUP_CONCAT(DISTINCT ?subject;SEPARATOR=',\n') AS ?datasetSubjects)
?otherDataset ?otherTitle ?otherDescription ?otherSpatialCoverage ?otherTemporalCoverage (GROUP_CONCAT(DISTINCT ?otherSubject;SEPARATOR=',\n') AS ?otherDatasetSubjects)
WHERE {
    ?dataset		a						dsv:Dataset ;
              		dct:title				?title ;
                	dct:description			?description ;
                 	schema:spatialCoverage	?spatialCoverage ;
                  	schema:temporalCoverage	?temporalCoverage ;
                   	dct:subject				?subject .
    ?otherDataset	a 						dsv:Dataset ;
                  	dct:title				?otherTitle ;
                   	dct:description 		?otherDescription ;
                    schema:spatialCoverage	?otherSpatialCoverage ;
                    schema:temporalCoverage	?otherTemporalCoverage ;
                    dct:subject				?otherSubject .
    VALUES ?dataset { <http://www.usa-open-data.org/example-dataset>}
    VALUES ?otherDataset { <http://www.uk-open-data.org/example-dataset> }
}
GROUP BY ?dataset ?title ?description ?spatialCoverage ?temporalCoverage ?otherDataset ?otherTitle ?otherDescription ?otherSpatialCoverage ?otherTemporalCoverage 

----

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dsv: <https://w3id.org/dsv-ontology#>

SELECT DISTINCT ?variable ?column ?columnProperty ?label ?description ?otherColumn ?otherColumnProperty ?otherLabel ?otherDescription

WHERE {
    ?dataset			a						dsv:Dataset ;
              			dsv:datasetSchema		[
        					dsv:column		?column ] .
	?column				dsv:columnProperty 		?columnProperty ;
						rdfs:label 				?label ;
						rdfs:description 		?description .
    ?columnProperty 	dsv:hasVariable 		?variable .
    
    ?otherDataset		a 						dsv:Dataset ;
                  		dsv:datasetSchema		[
        					dsv:column		?otherColumn ] .
    ?otherColumn		dsv:columnProperty 		?otherColumnProperty ;
						rdfs:label				?otherLabel ;
						rdfs:description 		?otherDescription .
    ?otherColumnProperty	dsv:hasVariable		?variable .

    VALUES ?dataset { <http://www.usa-open-data.org/example-dataset>}
    VALUES ?otherDataset { <http://www.uk-open-data.org/example-dataset> }
}


